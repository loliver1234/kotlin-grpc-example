group 'personal.kostera'
version '1.0-SNAPSHOT'

buildscript {
    ext {
        kotlinPluginVersion = '1.3.31'
        protobufPluginVersion = '0.8.8'
    }
    repositories {
        mavenCentral()
        jcenter()
        gradlePluginPortal()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinPluginVersion}")
        classpath("com.google.protobuf:protobuf-gradle-plugin:${protobufPluginVersion}")
        classpath("com.github.jengelman.gradle.plugins:shadow:2.0.4")
    }
}

apply plugin: 'java'
apply plugin: 'org.jetbrains.kotlin.jvm'
apply plugin: 'idea'
apply plugin: 'com.google.protobuf'
apply plugin: "com.github.johnrengelman.shadow"

sourceCompatibility = 1.8

repositories {
    google()
    jcenter()
    mavenCentral()
    mavenLocal()
}

def grpcVersion = '1.21.0'
def kotlinVersion = plugins.findPlugin("org.jetbrains.kotlin.jvm").kotlinPluginVersion


dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:${kotlinVersion}"
    implementation "org.jetbrains.kotlin:kotlin-reflect:${kotlinVersion}"
    implementation "io.grpc:grpc-netty-shaded:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    compileOnly "javax.annotation:javax.annotation-api:1.2"

    testImplementation "io.grpc:grpc-testing:${grpcVersion}"
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.25.1"
}

protobuf {
    protoc { artifact = 'com.google.protobuf:protoc:3.7.1' }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes(
                'Main-Class': 'personal.kostera.kotlin.grpc.poc.ApplicationMainKt'
        )
    }
}
